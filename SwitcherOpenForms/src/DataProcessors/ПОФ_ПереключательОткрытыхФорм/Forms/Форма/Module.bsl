// @strict-types

#Область ОписаниеПеременных

&НаКлиенте
Перем ИдентификаторыФорм; //см.КоллекцияИдентификаторовФорм

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОткрытыеФормы();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОткрытыеФормы

// Открытые формы выбор.
// 
// Параметры:
//  Элемент - ТаблицаФормы - Элемент
//  ВыбраннаяСтрока - Число - Выбранная строка
//  Поле - ПолеФормы - Поле
//  СтандартнаяОбработка - Булево - Стандартная обработка
&НаКлиенте
Процедура ОткрытыеФормыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	АктивироватьФорму();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Команда_Активировать(Команда)
	
	АктивироватьФорму();

КонецПроцедуры

&НаКлиенте
Процедура Команда_Закрыть(Команда)
	
	ЗакрытьВыбранныеФормы();
	ОбновитьОткрытыеФормы();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьОткрытыеФормы()
	
	ИдентификаторыФорм = КоллекцияИдентификаторовФорм();
	
	ОткрытыеФормы.Очистить();
	
	Для Каждого ОткрытоеОкно Из ПолучитьОкна() Цикл //ОкнаКлиентскогоПриложения
		
		Для Каждого ОткрытаяФорма Из ОткрытоеОкно.Содержимое Цикл //ФормаКлиентскогоПриложения
			
			Если ТипЗнч(ОткрытаяФорма) <> Тип("ФормаКлиентскогоПриложения") Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаголовокФормы = ОткрытоеОкно.Заголовок;
			
			СтрокаФормы = ОткрытыеФормы.Добавить();
			СтрокаФормы.Заголовок = ЗаголовокФормы;
			СтрокаФормы.ПолноеИмя = ОткрытаяФорма.ИмяФормы;
			СтрокаФормы.УникальныйИдентификатор = ОткрытаяФорма.УникальныйИдентификатор;
			СтрокаФормы.Активное = (ОткрытаяФорма.УникальныйИдентификатор = Параметры.ИдентификаторТекущейФормы);
			
			ИдентификаторыФорм.Вставить(ОткрытаяФорма.УникальныйИдентификатор, ОткрытаяФорма);
			
			Если СтрокаФормы.Активное Тогда
				Элементы.ОткрытыеФормы.ТекущаяСтрока = СтрокаФормы.ПолучитьИдентификатор();
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Активировать форму.
&НаКлиенте
Процедура АктивироватьФорму()
	
	ТекущиеДанные = Элементы.ОткрытыеФормы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ИдентификаторыФорм.Получить(ТекущиеДанные.УникальныйИдентификатор);
	Если Форма <> Неопределено Тогда
		Форма.Активизировать();
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

// Закрыть выбранные формы.
&НаКлиенте
Процедура ЗакрытьВыбранныеФормы()
	
	Для Каждого ВыделеннаяСтрока Из Элементы.ОткрытыеФормы.ВыделенныеСтроки Цикл //Число
		
		Строка = ОткрытыеФормы.НайтиПоИдентификатору(ВыделеннаяСтрока);
		Форма  = ИдентификаторыФорм.Получить(Строка.УникальныйИдентификатор);
		Если Форма <> Неопределено Тогда
			Форма.Закрыть();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Коллекция идентификаторов форм
// Метод для типизации ЕДТ
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - УникальныйИдентификатор
//  * Значение - ФормаКлиентскогоПриложения
&НаКлиенте
Функция КоллекцияИдентификаторовФорм()
	
	Возврат Новый Соответствие;
	
КонецФункции

#КонецОбласти
